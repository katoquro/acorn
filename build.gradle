plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

allprojects {
    apply plugin: 'groovy'

    group 'com.ainrif.acorn'
    version '0.5.0'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    java {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        all {
            it.exclude group: 'commons-logging'
            it.exclude group: 'org.apache.logging.log4j'
            it.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
    }

    dependencies {
        implementation platform("org.apache.groovy:groovy-bom:4.0.4")

        constraints {
            def slf4jVersion = '1.7.36'
            implementation "org.slf4j:slf4j-api:${slf4jVersion}"
            implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
            implementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
            implementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
            implementation "org.slf4j:osgi-over-slf4j:${slf4jVersion}"

            implementation 'info.picocli:picocli:4.6.3'
        }

        implementation 'org.slf4j:slf4j-api'
        implementation 'org.apache.groovy:groovy'

        compileOnly 'org.slf4j:jcl-over-slf4j'
        compileOnly 'org.slf4j:log4j-over-slf4j'
        compileOnly 'org.slf4j:osgi-over-slf4j'

        testImplementation platform('org.spockframework:spock-bom:2.2-M3-groovy-4.0')
        testImplementation 'org.spockframework:spock-core'
    }
}
